\chapter{Code}

\clearpage

\section{Implementation of the process interpreter using Cloud Haskell}
\label{app:distributed_split_slice}
\begin{lstlisting}[language=Haskell,frame=tb,numbers=left,caption=Implementation of \texttt{runProcess} using \textsf{Cloud Haskell}.]
runProcess :: Master -> Process a b -> a -> CH.Process b
runProcess _ Id x =
  return x

runProcess _ Err _ =
  return undefined

runProcess master (Basic sDict closureGen) x = do
  node <- getNode master
  res  <- call sDict node (closureGen x)
  returnNode master node
  return res

runProcess _ (Local p) x = do
  fakeMaster <- getFakeMaster
  res <- runProcess fakeMaster p x
  terminateMaster fakeMaster
  return res

runProcess master (Choice pr p1 p2) x = do
  b <- runProcess master pr x
  runProcess master (if b then p1 else p2) x

runProcess master (Sequence p1 p2) x =
  runProcess master p1 x >>= runProcess master p2

runProcess master (Parallel combinator p1 p2) x = do
  mvar <- liftIO newEmptyMVar
  _ <- spawnLocal $ runProcessHelper master p1 x mvar
  r2 <- runProcess master p2 x
  r1 <- liftIO $ takeMVar mvar
  runProcess master combinator (r1, r2)

runProcess master (Multilel ps fold) x = do
  mvars <- forM ps $ \_ -> liftIO newEmptyMVar
  mapM_ (\(proc, mvar) -> spawnLocal $ runProcessHelper master proc x mvar) (ps `zip` mvars)
  ress  <- forM mvars $ \mvar -> liftIO . takeMVar $ mvar
  runProcess master fold (x, ress)

runProcess master rep@(Repetition pr p) x =
  runProcess master (Choice pr (Sequence p rep) Id) x

runProcessHelper :: Master -> Process a b -> a -> MVar b -> CH.Process ()
runProcessHelper master p x mvar = do
  r <- runProcess master p x
  liftIO $ putMVar mvar r
\end{lstlisting}

\begin{landscape}
\section{Ant system processes}
\label{app:ant_system_processes}

\begin{lstlisting}[language=Haskell,frame=tb,numbers=left,caption=Implementation of basic processes for the ant system., basicstyle=\footnotesize\ttfamily]
ant :: Configuration -> CH.Process Solution
ant Configuration {..} = do
  path <- runAnt graph pheromones [1] (nodes graph \\ [1])
  return (path, pathLength' graph path)
    where
      runAnt :: Graph Int -> Pheromones -> [Node] -> [Node] -> CH.Process Path
      runAnt _ _ visited        [] = return visited
      runAnt g p visited unvisited = do
        let tau   = distance' p (last visited)
        let eta   = (1.0/) . fromIntegral . distance' g (last visited)
        let probs = [tau u**alpha * eta u**beta | u <- unvisited]
        rand <- liftIO $ randomRIO (0, sum probs)
        let next  = fst . head . dropWhile ((< rand) . snd) $ zip unvisited (scanl1 (+) probs)
        runAnt g p (visited ++ [next]) (unvisited \\ [next])

combinePaths :: (Configuration, [AntSolution]) -> CH.Process Configuration
combinePaths (conf@(Configuration {..}), ass) = return conf { pheromones = pheromones', path = path', pathLen = len' }
  where
    pheromones'   = depositPheromones ass pheromones
    (path', len') = minimumBy (compare `on` snd) ass

evaporations :: Configuration -> CH.Process Configuration
evaporations (conf@Configuration {..}) = return conf { pheromones = evaporation rho pheromones }

cycle :: Configuration -> CH.Process Configuration
cycle conf@Configuration {..} = return conf { iterations = iterations - 1 }

continue :: Configuration -> CH.Process Bool
continue Configuration {..} = return (iterations > 0)

extractSolution :: Configuration -> CH.Process Solution
extractSolution (Configuration {..}) = return (path, pathLen)
\end{lstlisting}

\end{landscape}